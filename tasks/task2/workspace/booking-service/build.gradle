plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.6'
    id 'io.spring.dependency-management' version '1.1.5'
    id 'com.google.protobuf' version '0.9.4'
}

group = 'com.hotelio'
version = '1.0.0'
java { toolchain { languageVersion = JavaLanguageVersion.of(17) } }
repositories { mavenCentral() }

ext {
    grpcVersion = '1.62.2'
    protobufVersion = '3.24.4'
    grpcSpringBootStarter = '2.15.0.RELEASE'
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.kafka:spring-kafka'
    runtimeOnly 'org.postgresql:postgresql'

    implementation "io.grpc:grpc-netty:${grpcVersion}"
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    implementation "com.google.protobuf:protobuf-java:${protobufVersion}"
    implementation "net.devh:grpc-server-spring-boot-starter:${grpcSpringBootStarter}"
    implementation 'org.springframework.boot:spring-boot-starter-json'

    compileOnly 'javax.annotation:javax.annotation-api:1.3.2'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:${protobufVersion}" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
    }
    generateProtoTasks {
        all().configureEach { task ->
            task.plugins { grpc {} }
        }
    }
}

tasks.withType(Test).configureEach { useJUnitPlatform() }